{
  description = "Nix flake for Doplarr (Rust)";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";
    crane.url = "github:ipetkov/crane";
    flake-utils.url = "github:numtide/flake-utils";
    rust-overlay = {
      url = "github:oxalica/rust-overlay";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    advisory-db = {
      url = "github:rustsec/advisory-db";
      flake = false;
    };
  };

  outputs = {
    self,
    nixpkgs,
    crane,
    flake-utils,
    rust-overlay,
    advisory-db,
    ...
  }:
    flake-utils.lib.eachDefaultSystem (
      system: let
        pkgs = import nixpkgs {
          inherit system;
          overlays = [(import rust-overlay)];
        };

        inherit (pkgs) lib;

        craneLib = (crane.mkLib pkgs).overrideToolchain (
          p:
            p.rust-bin.fromRustupToolchainFile ./rust-toolchain.toml
        );

        src = craneLib.cleanCargoSource ./.;

        commonArgs = {
          inherit src;
          strictDeps = true;
          nativeBuildInputs = with pkgs;
            [pkg-config]
            ++ lib.optionals stdenv.isDarwin [
              libiconv
            ];
          buildInputs = with pkgs; [openssl];
        };

        cargoArtifacts = craneLib.buildDepsOnly commonArgs;

        doplarr = craneLib.buildPackage (
          commonArgs
          // {
            pname = "doplarr";
            cargoExtraArgs = "-p doplarr";
          }
        );
      in {
        checks = {
          # Build the crates as part of `nix flake check` for convenience
          inherit doplarr;

          # Run clippy (and deny all warnings) on the doplarr package only.
          # We skip the api crates as they are autogenerated.
          clippy = craneLib.cargoClippy (
            commonArgs
            // {
              inherit cargoArtifacts;
              cargoClippyExtraArgs = "-p doplarr --all-targets -- --deny warnings --no-deps";
            }
          );

          # Check formatting
          fmt = craneLib.cargoFmt {
            inherit src;
          };

          toml-fmt = craneLib.taploFmt {
            src = pkgs.lib.sources.sourceFilesBySuffices src [".toml"];
            # taplo arguments can be further customized below as needed
            # taploExtraArgs = "--config ./taplo.toml";
          };

          # Audit dependencies
          audit = craneLib.cargoAudit {
            inherit src advisory-db;
          };
        };

        packages = {
          default = doplarr;

          # Docker image
          dockerImage = pkgs.dockerTools.buildLayeredImage {
            name = "ghcr.io/kiranshila/doplarr_rs";
            tag = "latest";

            contents = [doplarr];

            config = {
              Cmd = ["${doplarr}/bin/doplarr"];
              ExposedPorts = {};
              Env = [];
            };
          };
        };

        apps.default = flake-utils.lib.mkApp {
          drv = doplarr;
        };

        devShells.default = craneLib.devShell {
          # Inherit inputs from checks.
          checks = self.checks.${system};

          # Additional dev-shell environment variables can be set directly
          # MY_CUSTOM_DEVELOPMENT_VAR = "something else";

          # Extra inputs can be added here; cargo and rustc are provided by default.
          packages = [
            pkgs.openapi-generator-cli
          ];
        };
      }
    );
}
